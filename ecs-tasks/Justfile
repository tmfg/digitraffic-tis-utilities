# read env variables - mainly localstack override - from .env:
set dotenv-load := true

# Lists available Just recipes
default:
  @just --list --justfile {{justfile()}}

# Build rule container identified by its name.
build rulename:
  docker build --platform linux/amd64 -t ecs-task-{{rulename}} -f ./{{rulename}}/Dockerfile .

# Execute rule container identified by its name.
[private]
execute rulename:
  docker run --platform linux/amd64 \
    --rm \
    --read-only \
    --network=tis_tis \
    -v $(pwd)/app-tmp:/app/tmp \
    -e TMPDIR=/app/tmp \
    -e LOCALSTACK_ENDPOINT_URL=http://localstack:4566 \
    ecs-task-{{rulename}}

# Run specific rule identified by its directory name
run rule: (build rule) (execute rule)

# Shell into rule container. Useful for debugging.
shell rulename: (build rulename)
  docker run --platform linux/amd64 \
    --rm \
    -it \
    --entrypoint /bin/bash \
    ecs-task-{{rulename}}
