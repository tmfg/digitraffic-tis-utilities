# read env variables - mainly localstack override - from .env:
set dotenv-load := true

# Lists available Just recipes
default:
  @just --list --justfile {{justfile()}}

# Build rule container identified by its name.
[private]
build rulename:
  docker build -t ecs-task-{{rulename}} -f ./{{rulename}}/Dockerfile .

# Execute rule container identified by its name.
[private]
execute rulename:
  docker run \
    --rm \
    --network=tis_tis \
    -e LOCALSTACK_ENDPOINT_URL=http://localstack:4566 \
    ecs-task-{{rulename}}

# Run specific rule identified by its directory name
run rule: (build rule) (execute rule)

# Shell into rule container. Useful for debugging.
shell rulename: (build rulename)
  docker run \
    --rm \
    -it \
    --entrypoint /bin/bash \
    ecs-task-{{rulename}}
